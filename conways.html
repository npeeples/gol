<html>

<head>
  <style>
  #board {
    margin: 0 auto;
    border-collapse: collapse;
    display: table;
  }

  .row {
    white-space: nowrap;
  }

  .cell {
    margin: 0;
    padding: 0;
    border: 1px solid #999;
    float: left;
    width: 10px;
    height: 10px;
  }

  .alive {
    background-color: #111;
  }
  </style>

  <script>
  var require = {
    paths: {
      lodash: 'http://cdnjs.cloudflare.com/ajax/libs/lodash.js/1.0.0-rc.2/lodash.min',
      knockout: 'http://cdnjs.cloudflare.com/ajax/libs/knockout/2.2.0/knockout-min'
    }
  };
  </script>
  <script type="text/coffeescript">
  require ['knockout', 'lodash'], (ko, _) ->
    getParameterByName = (name) ->
      name = name.replace(/[\[]/, "\\[").replace(/[\]]/, "\\]")
      regexS = "[\\?&]" + name + "=([^&#]*)"
      regex = new RegExp(regexS)
      results = regex.exec(window.location.search)
      if results? then decodeURIComponent results[1].replace(/\+/g, " ") else ""

    splitparse = (on_, str) ->
      _.map str.split(on_), (i) -> parseInt i, 10

    construct = (rows, cols) ->
      grid = []
      i = 0

      while i < rows
        row = []
        j = 0

        while j < cols
          row.push new Cell((cols * i) + j)
          j += 1
        grid.push row
        i += 1
      grid

    toggle = (cell, neighbors) ->
      # lonliness
      return true if cell.alive() and neighbors < 2

      # overcrowding
      return true if cell.alive() and neighbors > 3

      # birth
      return true if not cell.alive() and neighbors is 3

      false

    run = (grid, width) ->
      length = grid.length
      toggles = []
      hints = [-1 * (width + 1), -1 * width, -1 * (width - 1), -1, 1, (width - 1), width, (width + 1)]

      _.chain(grid).each (cell, idx) ->
        collect = _.map hints, (i) ->
          n = idx + i

          if n < 0
            grid[length + n]
          else if n >= length
            grid[n - length]
          else
            grid[n]

        neighbors = _.reduce(collect, (num, neighbor) ->
          num += if neighbor.alive() then 1 else 0
        , 0)

        toggles.push idx if toggle cell, neighbors

      _.each toggles, (t) -> grid[t].toggle()

    class Cell
      constructor: (@idx) ->
        @alive = ko.observable(false)

      toggle: -> @alive not @alive()
      die: -> @alive false

    class ConwayView
      constructor: ->
        @isRunning = ko.observable(false)
        @interval = ko.observable(1000)

        @cols = ko.observable(20).extend(numeric: 0)
        @rows = ko.observable(10).extend(numeric: 0)
        @cells = ko.observableArray(construct(@rows(), @cols()))

        @seed = ko.observable()

        @cols.subscribe (val) => @cells construct(@rows(), val)
        @rows.subscribe (val) => @cells construct(val, @cols())

      run: ->
        timer = =>
          return unless @isRunning()
          run _.flatten(@cells()), @cols()
          setTimeout timer, @interval()

        @isRunning true
        setTimeout timer, 0

      stop: -> @isRunning false

      dump: ->
        @seed "&seed="
        _.each @cells(), (row, i) =>
          _.each row, (cell, j) =>
            if cell.alive()
              rnum = i + 1
              cnum = j + 1
              @seed @seed() + "#{rnum}:#{cnum},"

        @seed @seed().substr(0, @seed().length - 1)

      reset: ->
        @seed null
        _.each @cells(), (row, rnum) =>
          _.each row, (cell, cnum) => cell.die()

    ko.extenders.numeric = (target, precision) ->
      #create a writeable computed observable to intercept writes to our observable
      result = ko.computed(
        read: target #always return the original observables value
        write: (newValue) ->
          current = target()
          roundingMultiplier = Math.pow(10, precision)
          newValueAsNum = (if isNaN(newValue) then 0 else parseFloat(+newValue))
          valueToWrite = Math.round(newValueAsNum * roundingMultiplier) / roundingMultiplier
          if valueToWrite isnt current
            target valueToWrite
          else
            target.notifySubscribers valueToWrite  if newValue isnt current
      )
      result target()
      result

    init = (view) ->
      cols = getParameterByName("c")
      rows = getParameterByName("r")
      interval = getParameterByName("i")
      seed = getParameterByName("seed")

      view.cols cols if cols
      view.rows rows if rows
      view.interval interval if interval

      if seed
        view.seed "&seed=#{seed}"
        _.chain(seed.split(",")).map(_.partial(splitparse, ":")).each (xy) ->
          view.cells()[xy[0] - 1][xy[1] - 1].toggle()

      view

    ko.applyBindings init new ConwayView
  </script>
  <script src="http://cdnjs.cloudflare.com/ajax/libs/coffee-script/1.3.3/coffee-script.min.js"></script>
  <script src="http://cdnjs.cloudflare.com/ajax/libs/require.js/2.1.1/require.js"></script>
</head>

<body>
  <form>
    <button data-bind="click: run, disable: isRunning">Run</button>
    <button data-bind="click: stop, enable: isRunning">Stop</button>

    <label for="rows">rows</label>
    <input id="rows" type="text" data-bind="value: rows">
    <label for="cols">cols</label>
    <input id="cols" type="text" data-bind="value: cols">
    <label for="interval">interval</label>
    <input id="interval" type="text" data-bind="value: interval">

    <button data-bind="click: dump">Create Seed</button>
    <button data-bind="click: reset">Reset</button>
  </form>

  <div id="board" data-bind="foreach: cells">
    <div class="row" data-bind="foreach: $data">
      <span class="cell" data-bind="click: toggle, css: { alive: alive }"></span>
    </div>
  </div>

  <pre data-bind="text: seed, visible: seed"></pre>
</body>

</html>
